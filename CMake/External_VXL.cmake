# VXL External Project


if(fletch_ENABLE_GUI)
  set(VXL_ARGS_GUI
    -DBUILD_VGUI:BOOL=ON
    -DVGUI_USE_QT:BOOL=ON
    ${KWIVER_ARGS_QT}
    -DVGUI_USE_GLUT:BOOL=OFF
    -DVGUI_USE_GTK:BOOL=OFF
    -DVGUI_USE_GTK2:BOOL=OFF
    )
endif()

set(VXL_ARGS_CONTRIB
  -DBUILD_BRL:BOOL=OFF
  -DBUILD_MUL_TOOLS:BOOL=OFF
  -DBUILD_PRIP:BOOL=OFF
  )

set(VXL_ARGS_VIDL
  -DFFMPEG_CONFIG:FILEPATH=IGNORE
  -DFFMPEG_INCLUDE1_DIR:PATH=IGNORE
  -DFFMPEG_INCLUDE2_DIR:PATH=IGNORE
  -DFFMPEG_avcodec_LIBRARY:PATH=IGNORE
  -DFFMPEG_avformat_LIBRARY:PATH=IGNORE
  -DFFMPEG_avutil_LIBRARY:PATH=IGNORE
  -DFFMPEG_swscale_LIBRARY:PATH=IGNORE
  )

if(UNIX)
  set(VXL_ARGS_V3P
    -DVXL_USING_NATIVE_ZLIB:BOOL=TRUE
    -DVXL_USING_NATIVE_BZLIB2:BOOL=TRUE
    )
  if(APPLE)
    set(VXL_EXTRA_CMAKE_CXX_FLAGS "--std=c++11 -stdlib=libc++ -D__STDC_CONSTANT_MACROS ")
  else()
    set(VXL_EXTRA_CMAKE_CXX_FLAGS -D__STDC_CONSTANT_MACROS)
  endif()
elseif(WIN32)
  set(VXL_ARGS_V3P
    -DVXL_FORCE_V3P_JPEG:BOOL=ON
    -DVXL_FORCE_V3P_TIFF:BOOL=ON
    )
endif()

ExternalProject_Add(VXL
  PREFIX ${fletch_BUILD_PREFIX}
  URL ${VXL_url}
  URL_MD5 ${VXL_md5}

  CMAKE_GENERATOR ${gen}
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX:PATH=${fletch_BUILD_INSTALL_PREFIX}
    -DCMAKE_CXX_COMPILER:PATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER:PATH=${CMAKE_C_COMPILER}
    -DBOOST_ROOT:PATH=${BOOST_ROOT}
    ${VXL_ARGS_GUI}
    ${VXL_ARGS_CONTRIB}
    ${VXL_ARGS_VIDL}
    ${VXL_ARGS_V3P}
    "-DVXL_EXTRA_CMAKE_CXX_FLAGS:STRING=${VXL_EXTRA_CMAKE_CXX_FLAGS}"
	  -DCMAKE_CXX_STANDARD:STRING=11
    -DBUILD_EXAMPLES:BOOL=OFF
    -DBUILD_TESTING:BOOL=OFF
    -DBUILD_DOCUMENTATION:BOOL=OFF
	  -DBUILD_CORE_PROBABILITY:BOOL=ON
    -DBUILD_CORE_GEOMETRY:BOOL=ON
    -DBUILD_CORE_NUMERICS:BOOL=ON
    -DBUILD_CORE_IMAGING:BOOL=ON
    -DBUILD_CORE_SERIALISATION:BOOL=ON
    -DBUILD_GEL:BOOL=OFF
    -DBUILD_MUL:BOOL=OFF
    -DBUILD_MUL_TOOLS:BOOL=OFF
    -DBUILD_TBL:BOOL=OFF
    # Forcing off the use of OpenGL
    -DOPENGL_xmesa_INCLUDE_DIR:PATH=
  INSTALL_DIR ${fletch_BUILD_INSTALL_PREFIX}
  )

#ExternalProject_Add_Step(VXL forcebuild
#  COMMAND ${CMAKE_COMMAND}
#    -E remove ${KWIVER_BUILD_PREFIX}/src/VXL-stamp/VXL-build
#  COMMENT "Removing build stamp file for build update (forcebuild)."
#  DEPENDEES configure
#  DEPENDERS build
#  ALWAYS 1
#  )

include_directories( SYSTEM ${fletch_BUILD_INSTALL_PREFIX}/include/vxl
                            ${fletch_BUILD_INSTALL_PREFIX}/include/vxl/vcl
                            ${fletch_BUILD_INSTALL_PREFIX}/include/vxl/core )

set(KWIVER_ARGS_VXL
  -DVXL_DIR:PATH=${fletch_BUILD_INSTALL_PREFIX}/share/vxl/cmake
  )
